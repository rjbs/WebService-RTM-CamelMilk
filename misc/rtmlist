#!perl
use v5.20.0;
use warnings;

use lib 'lib';

binmode *STDOUT, ':encoding(UTF-8)';

use JSON::MaybeXS;
use Path::Tiny;
use WebService::RTM::CamelMilk;
use WebService::RTM::CamelMilk::AuthMgr::Dir;

my $path = $ENV{CAMEL_MILK_CONFIG};

die "no CAMEL_MILK_CONFIG\n" unless length $path;

my $authmgr = WebService::RTM::CamelMilk::AuthMgr::Dir->new({
  dir => $path
});

die "usage: rtmlist USERNAME\n" if @ARGV > 1;

my $username = $ARGV[0];

my $auth;
if ($username) {
  $auth = $authmgr->auth_for_username($username);
  die "can't get token for username $username\n" unless $auth;
} elsif (my $default = $authmgr->config->{default_user}) {
  $auth = $authmgr->tokens->{ $default };
  die "default token no longer exists\n" unless $auth;
} else {
  die "no username given and no default user\n";
}

my $milk = WebService::RTM::CamelMilk->new_standalone({
  api_key     => $authmgr->config->{api_key},
  api_secret  => $authmgr->config->{api_secret},
});

my $rsp = $milk->api_call('rtm.tasks.getList' => {
  auth_token => $auth->{token},
  filter     => 'status:incomplete',
})->get;

die "it failed\n" unless $rsp->is_success;

print JSON::MaybeXS->new->pretty->canonical->encode($rsp->_response);
